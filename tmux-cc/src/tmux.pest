number = { ASCII_DIGIT+ }
any_text = { ANY* }

pane_id = { "%" ~ number }
window_id = { "@" ~ number }
session_id = { "$" ~ number }

begin = { "%begin " ~ number ~ " " ~ number ~ " " ~ number }
end = { "%end " ~ number ~ " " ~ number ~ " " ~ number }
error = { "%error " ~ number ~ " " ~ number ~ " " ~ number }
output = { "%output " ~ pane_id ~ " " ~ any_text }
exit = { "%exit" }
sessions_changed = { "%sessions-changed" }
pane_mode_changed = { "%pane-mode-changed " ~ pane_id }
window_add = { "%window-add " ~ window_id }
session_changed = { "%session-changed " ~ session_id ~ " " ~ any_text }

line = _{ (
  begin |
  end |
  error |
  exit |
  output |
  pane_mode_changed |
  session_changed |
  sessions_changed |
  window_add
) }

line_entire = _{ SOI ~ line ~ EOI }
